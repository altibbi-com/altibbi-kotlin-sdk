plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

ext {
    altibbiTelehealthVersion = '0.1.6'
}

android {
    namespace 'com.altibbi.telehealth'
    compileSdk 35

    defaultConfig {
        minSdk 24
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.11.0'
    implementation 'com.pusher:pusher-java-client:2.4.4'
    api 'com.sendbird.sdk:sendbird-android-sdk:3.1.27'
    api 'com.opentok.android:opentok-android-sdk:2.28.0'
    api 'com.squareup.okhttp3:okhttp:4.12.0'

    // Kotlin Coroutines for asynchronous tasks
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"

    // Testing libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    include '**/*.java', '**/*.kt'  // Include Java and Kotlin source files
}

// Ensure sourceJar runs before metadata generation
afterEvaluate {
    tasks.named('generateMetadataFileForReleasePublication').configure {
        dependsOn sourceJar
    }
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = 'test'
                password = 'test'
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = 'com.altibbi.telehealth'
            artifactId = 'AltibbiTelehealth'
            version = altibbiTelehealthVersion

            afterEvaluate {
                from components.release
            }

            artifact sourceJar

            pom {
                packaging = 'aar'
                name = 'Altibbi Telehealth Library'
                description = 'A library providing telehealth functionalities for Altibbi applications.'
                url = 'https://github.com/altibbi-com/altibbi-kotlin-sdk'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mahmoud.shtayyat'
                        name = 'Mahmoud Alshtayyat'
                        email = 'mahmoud@altibbi.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/altibbi-com/altibbi-kotlin-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:altibbi-com/altibbi-kotlin-sdk.git'
                    url = 'http://github.com/altibbi-com/altibbi-kotlin-sdk'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}