plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

ext {
    altibbiTelehealthVersion = '0.1.4'
}

android {
    namespace 'com.altibbi.telehealth'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.10.1'
    implementation 'com.pusher:pusher-java-client:2.4.4'
    api 'com.sendbird.sdk:sendbird-android-sdk:3.1.27'
    api 'com.opentok.android:opentok-android-sdk:2.26.1'
    api 'com.squareup.okhttp3:okhttp:4.10.0'

    // Kotlin Coroutines for asynchronous tasks
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    // Testing libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    include '**/*.java', '**/*.kt'  // Include Java and Kotlin source files
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = 'test'
                password = 'test'
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = 'com.altibbi.telehealth'
            artifactId = 'AltibbiTelehealth'
            version = altibbiTelehealthVersion

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            artifact sourceJar

            pom {
                name = 'Altibbi Telehealth Library'
                description = 'A library providing telehealth functionalities for Altibbi applications.'
                url = 'https://github.com/altibbi-com/altibbi-kotlin-sdk'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mahmoud.shtayyat'
                        name = 'Mahmoud Alshtayyat'
                        email = 'mahmoud@altibbi.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/altibbi-com/altibbi-kotlin-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:altibbi-com/altibbi-kotlin-sdk.git'
                    url = 'http://github.com/altibbi-com/altibbi-kotlin-sdk'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}